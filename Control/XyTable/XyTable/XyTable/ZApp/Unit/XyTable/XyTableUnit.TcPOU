<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="XyTableUnit" Id="{2d0b52a7-e0e5-4970-8cf8-853c678bbcc0}" SpecialFunc="None">
    <Declaration><![CDATA[/// Declaration of a distinct unit of this application
FUNCTION_BLOCK XyTableUnit EXTENDS ZApplication.Unit IMPLEMENTS IXyTable
VAR
  _equipment : XyTableEquipment(THIS^);
  _data : XyTableDataRef;
  _com : XyTableComRef;

  _boot : XyTableSequenceBoot(THIS^);
  _automatic : XyTableSequenceAutomatic(THIS^);
  _stop : XyTableSequenceStop(THIS^);
  _gohome : XyTableSequenceGoHome(THIS^);
  _faultReaction : XyTableSequenceFaultReaction(THIS^);
  
  _statemachine : ZApplication.UnitStateMachine;

  _observeTimer : ZAux.Timer;
  
  _segments : RingBufferSegment(
                10, 
                ADR(ZGlobal.Com.Unit.XyTable.Subscribe.Segments.Start),
                ADR(ZGlobal.Com.Unit.XyTable.Subscribe.Segments.End),
                ADR(ZGlobal.Com.Unit.XyTable.Subscribe.Segments.Data));      
  
END_VAR

{attribute 'ZId' := '58bf1f70-3606-4d15-9092-18e6bfacf7a1'}]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF _observeTimer.Done THEN
  _observeTimer.WaitAsync(0.1);
  
  // monitor _com for requested actions (e.g. start homing, start automatic mode, ...)
  ObserveRequest(_com.Subscribe.Request.Start, ZApplication.UnitStateMachineState.Automatic);
  ObserveRequest(_com.Subscribe.Request.Stop, ZApplication.UnitStateMachineState.Stop);
  ObserveRequest(_com.Subscribe.Request.GoHome, ZApplication.UnitStateMachineState.GoHome);
  ObserveRequest(_com.Subscribe.Request.Halt, ZApplication.UnitStateMachineState.Halt);
  
  // provide relevant information via ADS
  _com.Publish.Request.Stop := _stateMachine.IsTransitionAllowed(ZApplication.UnitStateMachineState.Stop);
  _com.Publish.Request.Start := _stateMachine.IsTransitionAllowed(ZApplication.UnitStateMachineState.Automatic);
  _com.Publish.Request.GoHome := _stateMachine.IsTransitionAllowed(ZApplication.UnitStateMachineState.GoHome);
  _com.Publish.Request.Halt := _stateMachine.IsTransitionAllowed(ZApplication.UnitStateMachineState.Halt);

  _com.Publish.State := _stateMachine.State();
END_IF]]></ST>
    </Implementation>
    <Folder Name="UnitInterface" Id="{cc8e9e75-4264-4a4e-9cd1-5deaebb223f0}" />
    <Method Name="FB_init" Id="{82b397b3-30e4-4e1d-8a76-d98dc227b714}">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
  bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
  bInCopyCode : BOOL;  // if TRUE, the instance afterwards gets moved into the copy code (online change)
  parent : ZCore.IManagedObject; // caller of the Unit's cyclic method. If the Unit runs in its own context use 0
  name : ZCore.ZString;  // name of this unit as it should show up in the logger
  configdata : REFERENCE TO XyTableDataConfig;  // reference to the configuration data that is used during boot to configure the Units equipment
  machinedata : REFERENCE TO XyTableDataMachine; // reference to the machine data that is used through most sequences of the unit (motor speed, axes positions, ...)
  calibrationdata : REFERENCE TO XyTableDataCalibration;  // reference to calibration that is determined by calibration sequences of the unit (static offsets, shifts)
  com : REFERENCE TO XyTableCom;  // reference to the communication interface that is used to control this Unit via ADS
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[_name := name;

// initialize local logger decorator
_loggerDecorator.FB_init(bInitRetains, bInCopyCode, name, ZModuleProgram.Logger);
_alarmingDecorator.FB_init(bInitRetains, bInCopyCode, name, ZModuleProgram.Alarming);

// setup data
_data.Config REF= configdata;
_data.Machine REF= machinedata;
_data.Calibration REF= calibrationdata;
_com.Publish REF= com.Publish;
_com.Subscribe REF= com.Subscribe;

// ------- sequence initialization, map sequences of this unit to the statemachine -------
{attribute '__ZwPlcUnitInitSequenceImplementation__'} // Do not remove this attribute! It is used for code generation.
_statemachine.SetSequence(ZApplication.UnitStateMachineState.Boot, _boot);
_statemachine.SetSequence(ZApplication.UnitStateMachineState.FaultReaction, _faultReaction);
_statemachine.SetSequence(ZApplication.UnitStateMachineState.Gohome, _gohome);
_statemachine.SetSequence(ZApplication.UnitStateMachineState.Stop, _stop);
_statemachine.SetSequence(ZApplication.UnitStateMachineState.Automatic, _automatic);

_statemachine.SetName(_name);
_statemachine.SetLogger(_logger);

// set initial state
_statemachine.SetState(ZApplication.UnitStateMachineState.Boot);
SetStateMachine(_statemachine);]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>